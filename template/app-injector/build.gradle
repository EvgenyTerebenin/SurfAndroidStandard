buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
//todo uncoment for real app
//apply plugin: 'io.fabric'

apply from: '../commonTestModule.gradle'
apply from: '../config.gradle'

apply from: '../keystore/signinConfigs.gradle'
apply from: '../buildTypes.gradle'
apply from: '../commonAndroidDefaultConfig.gradle'

def config = extensions.getByName("ext")

android {

    defaultConfig {
        applicationId config["applicationId"]

        versionCode config["versionCode"]
        versionName config["versionName"]

        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "$applicationName-$versionName-($versionCode)")

        testBuildType 'debug' //todo replace to 'qa' for real app
    }
}

dependencies {
    implementation project(localModules.base)
    implementation project(localModules.baseUi)
    implementation project(localModules.iInitialization)
    implementation project(localModules.iNetwork)
    implementation project(localModules.iToken)
    implementation project(localModules.iSession)
    implementation project(localModules.iAuth)
    implementation project(localModules.fAuth)
    implementation project(localModules.fMain)
    implementation project(localModules.fSplash)

    //debug
    releaseImplementation project(localModules.fDebugNoOp)
    debugImplementation project(localModules.fDebug)
    qaImplementation project(localModules.fDebug)

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    implementation "androidx.media:media:$androidxVersion"
    implementation "androidx.legacy:legacy-support-v4:$androidxVersion"

    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    implementation "com.google.firebase:firebase-messaging:$firebaseMessagingVersion"

    api("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion@aar") {
        transitive = true
    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 1, 'seconds'
    resolutionStrategy.cacheChangingModulesFor 1, 'seconds'
}

//todo uncoment for real app
//apply plugin: 'com.google.gms.google-services'