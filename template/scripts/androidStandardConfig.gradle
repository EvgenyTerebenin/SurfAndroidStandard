apply from: '../scripts/androidStandardModules.gradle'

// Скрипт для чтения и экспорта properties для дальнейшей настройки сборки

// Флаг для включения режима локальной загрузки
def androidStandardDebugMode = false

Properties properties = new Properties()

try {
    properties.load(file('../local.properties').newDataInputStream())
} catch (FileNotFoundException ignored) {
    logger.error('local.properties file not found')
}

// Директория для локальной загрузки android-standard
def androidStandardDebugDir = properties.getProperty('androidStandardDebugDir')
if (androidStandardDebugDir == null) {
    logger.error('androidStandardDebugDir not defined. ' +
            'Please define it in local.properties file to enable android-standard debug mode.')
} else {
    def debugModeProperty = properties.getProperty('androidStandardDebugMode')
    if (debugModeProperty != null) {
        androidStandardDebugMode = debugModeProperty.toBoolean()
    }
    if (!androidStandardDebugMode) {
        logger.error('androidStandardDebugMode = false')
    }
}

// Экспорт переменных, считанных из properties
gradle.ext.androidStandardDebugDir = androidStandardDebugDir
gradle.ext.androidStandardDebugMode = androidStandardDebugMode

gradle.ext {
    // Функция, добавляющая префикс "debug" к именам модулей android-standard,
    // если они загружаются локально
    androidStandardDebugModuleName = { name -> "debug-$name" }

    // Флаг, проверяющий, активирован ли режим локальной загрузки модулей android-standard
    androidStandardDebugEnabled = gradle.ext.androidStandardDebugDir != null && gradle.ext.androidStandardDebugMode
}