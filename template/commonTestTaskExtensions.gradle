import java.nio.file.Files
import java.nio.file.Paths

final String TEST_TYPE_PROPERTY_NAME = "testType"
final String TEST_NAME_PATTERN = "^test\\w*UnitTest\$"

tasks.whenTaskAdded { task ->
    if (task.name.matches(TEST_NAME_PATTERN)) {

        if (project.hasProperty(TEST_TYPE_PROPERTY_NAME)) {

            String ps = project.property(TEST_TYPE_PROPERTY_NAME)
            List<String> testType = ps.split(",")

            task.doFirst {
                List<String> testClasses = []
                android.sourceSets.test.getJavaDirectories().forEach { testDir ->
                    if (testDir.exists()) {
                        String prefix = "${testDir}/"
                        testDir.traverse { File testFile ->
                            if (testFile.absolutePath.endsWith('.kt') || testFile.absolutePath.endsWith('.java')) {
                                String testContent = new String(Files.readAllBytes(Paths.get(testFile.absolutePath)))
                                if (testContent.contains("@Test")) {
                                    if (testType.contains("unit")) {
                                        if (!testContent.contains("@RunWith(ApiTestRunner")) {
                                            testClasses.add(Utils.getTestClassName(testFile, prefix))
                                        }
                                    }

                                    if (testType.contains("api") || testType.contains("waitApi")) {
                                        if (testContent.contains("@RunWith(ApiTestRunner")) {
                                            testClasses.add(Utils.getTestClassName(testFile, prefix))

                                            if (testType.contains("api")) systemProperty "check_api", "true"
                                            if (testType.contains("waitApi")) systemProperty "wait_api", "true"
                                        }
                                    }
                                }
                            }
                        }
                    }
                }
                include(testClasses)
            }
        }
    }
}

class Utils {
    static String getTestClassName(File testFile, String prefix) {
        (testFile.absolutePath - prefix).replace(".kt", ".*").replace(".java", ".*")
    }
}